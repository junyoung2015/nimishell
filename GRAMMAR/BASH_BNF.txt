<ALPHA> ::= a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|
             A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z

<DIGIT> ::= 0|1|2|3|4|5|6|7|8|9

<NUMBER> ::= <DIGIT>
           | <NUMBER> <DIGIT>

<WORD> ::= <ALPHA>
         | <WORD> <ALPHA>
         | <WORD> '_'

<ENV-VAR> ::= '$' <WORD>

<WORD-LIST> ::= <WORD>
             |  <WORD-LIST> <WORD>

<ASSIGNMENT-WORD> ::= <WORD> '=' <WORD>

<REDIRECTION> ::=  '>' <WORD>
                |  '<' <WORD>
                |  '>>' <WORD>
                |  '<<' <WORD>

<SIMPLE-COMMAND-ELEMENT> ::= <WORD>
                          |  <ASSIGNMENT-WORD>
                          |  <REDIRECTION>
                          |  <ENV-VAR>

<REDIRECTION-LIST> ::= <REDIRECTION>
                    |  <REDIRECTION-LIST> <REDIRECTION>

<SIMPLE-COMMAND> ::=  <SIMPLE-COMMAND-ELEMENT>
                   |  <SIMPLE-COMMAND> <SIMPLE-COMMAND-ELEMENT>

<COMMAND> ::=  <SIMPLE-COMMAND>
            |  <SUBSHELL>
            |  <SUBSHELL> <REDIRECTION-LIST>

<SUBSHELL> ::=  '(' <LIST> ')'

<PATTERN> ::=  <WORD>
            |  <PATTERN> '|' <WORD>

<LIST> ::=   <LIST> '&&' <LIST>
           |  <LIST> '||' <LIST>
           |  <PIPELINE>

<SIMPLE-LIST> ::=  <SIMPLE-LIST> '&&' <SIMPLE-LIST>
                 |  <SIMPLE-LIST> '||' <SIMPLE-LIST>
                 |  <PIPELINE>

<PIPELINE> ::=  <PIPELINE> '|' <PIPELINE>
             |  <COMMAND>